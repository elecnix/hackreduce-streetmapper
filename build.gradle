version = '0.2'

// Modify these values if you want to build against different versions of Hadoop
hadoop_version = '0.20.2'


apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'org.hackreduce'

sourceCompatibility = 1.6

defaultTasks "clean", "jar"

jar {
  manifest {
      attributes 'Implementation-Title': 'HackReduce', 'Implementation-Version': version
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.apache.hadoop:hadoop-core:' + hadoop_version
  compile 'org.apache.hadoop:hadoop-streaming:' + hadoop_version
  compile 'org.apache.hadoop:hadoop-tools:' + hadoop_version
  compile 'org.apache.hadoop:hadoop-test:' + hadoop_version
}

def pomConfig = {
  name 'HackReduce'
  description 'HackReduce related code'
  url 'http://hackreduce.org'
  organization {
    name 'Hopper Travel'
    url 'http://hopper.travel'
  }
  issueManagement {
    system 'Github'
    url 'https://github.com/hoppertravel/HackReduce/issues'
  }
  scm {
    url "https://github.com/hoppertravel/HackReduce"
    connection "scm:git:http://github.com/hoppertravel/HackReduce.git"
    developerConnection "scm:git:git@github.com:hoppertravel/HackReduce.git"
  }
  licenses {
    license {
      name 'MIT License'
      url 'http://www.opensource.org/licenses/mit-license.php'
      distribution 'repo'
    }
  }
  developers {
    developer {
      id 'hopper-team'
      name 'The Hopper Travel Team'
      organization 'Hopper Travel'
      organizationUrl 'http://hopper.travel'
    }
  }
}

configure(install.repositories.mavenInstaller) {
  pom.project pomConfig
}

task sourcesJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives sourcesJar
}
